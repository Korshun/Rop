//Sounds: Midway Games, Blizzard
//Sprites: Id Software, Eriance
//Projectile Sprites: Id Software, Jimmy, Skulltag Team

Actor TormentedMancubus : Mancubus 
{
  Health 950
  Speed 9
  PainChance 65
  Scale 1.125
  SeeSound "Psxfatso/sight"
  PainSound "Psxfatso/pain"
  DeathSound "Psxfatso/death"
  ActiveSound "Psxpinky/act"
  Obituary "%o took a glance at a torment's weapons."
  DamageFactor "TormentBfg", 0
  Species "Fatties"
  BloodColor Green
  States
  {
  Spawn:
    FAT2 AB 15 A_Look
    Loop
  See:
    FAT2 AABBCCDDEEFF 4 A_Chase
    Loop
  Missile:
    FAT2 A 0 A_FaceTarget
    FAT2 G 20 A_PlaySound("PsxFatso/Attack")
	FAT2 A 0 A_FaceTarget
	FAT2 A 0 A_Jump(128, "AltAttack")
    FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, 0)
	FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, -7.5)
    FAT2 H 10 Bright A_CustomMissile("TormentShot", 32, 0, -15)
    FAT2 IG 5 A_FaceTarget
    FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, 0)
	FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, 7.5)
    FAT2 H 10 Bright A_CustomMissile("TormentShot", 32, 0, 15)
    FAT2 IG 5 A_FaceTarget
    FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, 0)
	FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, 7.5)
    FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, 15)
	FAT2 H 0 Bright A_CustomMissile("TormentShot", 32, 0, -7.5)
    FAT2 H 10 Bright A_CustomMissile("TormentShot", 32, 0, -15)
    FAT2 IG 5 A_FaceTarget
    Goto See
  AltAttack:
    FAT2 H 0 Bright A_CustomMissile("TormentBfgBall", 24, -24, 0)
    FAT2 H 10 Bright A_CustomMissile("TormentBfgBall", 24, 24, -5)
    FAT2 IG 8 A_FaceTarget
	FAT2 H 0 Bright A_CustomMissile("TormentBfgBall", 24, -24, 5)
    FAT2 H 10 Bright A_CustomMissile("TormentBfgBall", 24, 24, 0)
    FAT2 IG 8 A_FaceTarget
	Goto See
  Pain:
    FAT2 J 3
    FAT2 J 3 A_Pain
    Goto See
  Death:
    FAT2 K 6
    FAT2 L 6 A_Scream
    FAT2 M 6 A_NoBlocking
    FAT2 NOPQRS 6
    FAT2 T -1 A_KillMaster
    Stop
  Raise:
    FAT2 R 5
    FAT2 QPONMLK 5
    Goto See
  }
}

Actor TormentShot : NewFatShot
{
  States
  {
  Spawn:
    TORF AB 4 Bright
	Loop
  Death:
    TORF C 8 Bright
	TORF D 6 Bright
	TORF E 4 Bright
	Stop
  }
}

Actor TormentBfgBall : TormentShot
{
  DamageType "TormentBfg"
  SeeSound "HectBfg/Fire"
  DeathSound "HectBfg/Death"
  Damage 0
  Scale 0.68
  +ForceRadiusDmg
  States
  {
  Spawn:
    LPRJ ABCDEFGHIJ 2 Bright
	Loop
  Death:
    TNT1 A 0 A_Explode(64, 96, 0)
	TNT1 A 1 A_SpawnItemEx("TormentBfgDeath")
	Stop
  }
}

Actor TormentBfgDeath 
{
  +NoInteraction
  +ClientSideOnly
  RenderStyle Add
  States
  {
  Spawn:
    LPRJ KLMNOPQRSTUVW 2 A_FadeOut
	Stop
  }
}