//Code: Skulltag Team, rewritten by Neon
//Sprites: Eriance, recolored by Neon
//Sounds: Eriance, Midway Games

Actor Abaddon : Cacodemon2
{
  Health 1000
  PainChance 56
  Scale 1.15
  SeeSound "Beholder/Sight"
  DeathSound "Beholder/Death"
  Obituary "%o was turned into ashes by an abaddon."
  HitObituary "%o was bitten by an abaddon."
  DropItem "ShieldAmmo", 128, 5
  BloodColor "darkBlue"
  States
  {
  Spawn:
    ABDN ABCDCB 3 A_Look
	Loop
  See:
    ABDN ABCDCB 3 A_Chase
	Loop
  Melee:
  Missile:
    ABDN HI 5 A_FaceTarget
	ABDN J 0 A_CustomMissile("AbaddonBall", 32, 0, 0)
	ABDN J 0 A_CustomMissile("AbaddonWeaver2", 32, 0, 0)
	ABDN J 5 Bright A_CustomComboAttack("AbaddonWeaver1", 32, 10 * random(4, 9))
	Goto See
  Pain:
    ABDN K 3
	ABDN K 3 A_Pain
	Goto See
  Death: 
    ABDN L 8 A_NoBlocking
    ABDN M 8 A_Scream
    ABDN NOPQ 8
    TNT1 A -1 A_SetFloorClip
    Stop
  Raise:
    TNT1 L 8 A_UnsetFloorClip
    ABDN QPONML 8
    Goto See
  }
}

Actor AbaddonBall : CacoDemonBall
{
  Decal CacoScorch
  DamageType "Monster"
  Damage (Random(22, 32))
  Speed 22
  States
  {
  Spawn:
    LBAL AB 4 Bright A_SpawnItemEx("LanternBallTrail")
	Loop
  Death:
    LBAL CDE 6 Bright
	Stop
  }
}

Actor AbaddonWeaver1 : AbaddonBall
{
  States
  {
  Spawn:
    LBAL ABAB 1 Bright A_Weave(3, 0, 1.5, 0.0)
	LBAL A 0 A_SpawnItemEx("LanternBallTrail")
	Loop
  }
}

Actor AbaddonWeaver2 : AbaddonBall
{
  States
  {
  Spawn:
    LBAL ABAB 1 Bright A_Weave(-3, 0, 1.5, 0.0)
	LBAL A 0 A_SpawnItemEx("LanternBallTrail")
	Loop
  }
}