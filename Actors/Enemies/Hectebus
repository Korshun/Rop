//Sprites: Skulltag Team
//Sounds: Midway Games
//Decorate: Neon

Actor Hectebus
{
  Health 1100
  Scale 1.125
  Radius 48
  Height 64
  Mass 1000
  Speed 8
  Monster
  +FLOORCLIP
  PainChance 20
  Obituary "%o was melted by a hectebus' plasma."
  SeeSound "PsxFatso/Sight"
  PainSound "PsxFatso/Pain"
  ActiveSound "PsxPinky/ACT"
  DeathSound "PsxFatso/Death"
  BloodColor "DarkRed"
  Species "Fatties"
  DropItem "ShieldAmmo", 64, 5
  States
  {
  Spawn:
    HECT AB 15 A_Look
    Loop
  See:
    HECT AABBCCDDEEFF 4 A_Chase
    Loop
  Pain:
    HECT J 3
    HECT J 3 A_Pain
    Goto See
  Missile:
    HECT G 0 A_PlaySound("Psxfatso/Attack")
    HECT G 20 A_FaceTarget
    HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,16.875,1)
    HECT H 10 Bright
    HECT IG 5 A_FaceTarget
    HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,-16.875,1)
    HECT H 10 Bright
    HECT IG 5 A_FaceTarget
    HECT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
    HECT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
    HECT H 10 Bright
    HECT IG 5 A_FaceTarget
    Goto See
  Death:
    HECT K 6
    HECT L 6 A_Scream
    HECT M 6 A_NoBlocking
    HECT NOPQRS 6
    HECT T -1 A_KillMaster
    Stop
  Raise:
    HECT RQPONMLK 5
    Goto See
  }
}
  
Actor HectShot : FatShot
{
  Speed 22
  Damage 12
  DamageType "Monster"
  States
  {
  Spawn:
    TORF AB 4 Bright
    Loop
  Death:
    TORF C 8 Bright
    TORF D 6 Bright
    TORF E 4 Bright
    Stop
  }
}