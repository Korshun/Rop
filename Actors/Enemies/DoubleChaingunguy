//Decorate: Eriance
//GLDefs: Ghastly Dragon
//Sounds: Midway Games
//Sprites: Id Software, Eriance

Actor DoubleChaingunguy : Chaingunguy2
{
  Health 110
  PainChance 100
  Scale 1.1
  Obituary "A double chaingunner made %o dance to his tune."
  SeeSound "PsxZombie/sight"
  PainSound "PsxZombie/pain"
  DeathSound "PsxZombie/death"
  ActiveSound "PsxZombie/active"
  States
  {
  Spawn:
    DPOS AB 10 A_Look
    Loop
  See:
    DPOS AABBCCDD 3 A_Chase
    Loop
  Missile:
    DPOS E 10 A_FaceTarget
    DPOS E 2 Bright A_CustomBulletAttack(11.25, 11.25, 2, Random(1, 3)*5, "MonsterBulletPuff", 0, CBAF_NORANDOM)
	DPOS F 2 Bright
    DPOS E 2 Bright A_CustomBulletAttack(11.25, 11.25, 2, Random(1, 3)*5, "MonsterBulletPuff", 0, CBAF_NORANDOM)
	DPOS F 2 Bright
    DPOS F 1 A_CPosRefire
    Goto Missile+1
  Pain:
    DPOS G 3
    DPOS G 3 A_Pain
    Goto See
  Death:
    DPOS H 5
    DPOS I 5 A_Scream
    DPOS J 5 A_NoBlocking
    DPOS KLM 5
    DPOS N -1
    Stop
  XDeath:
    DPOS O 5
    DPOS P 5 A_XScream
    DPOS Q 5 A_NoBlocking
    DPOS RS 5
    DPOS T -1
    Stop
  Raise:
    DPOS NMLKJIH 5
    Goto See
  }
}