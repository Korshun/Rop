Actor Zombieman2 : Zombieman
{
  PainChance 150
  Obituary "%o was peppered by a zombieman's pea shooter."
  +QuickToRetaliate
  States
  {
  See:
    POSS AABBCCDD 3 A_Chase
	Loop
  Missile:
    POSS A 0 A_JumpIfCloser(256, "3shotburst")
  1shot:
	POSS E 8 A_FaceTarget
	POSS F 6 Bright A_CustomBulletAttack(12.5, 12.5, 1, Random(3, 9), "MonsterBulletPuff", 0, Cbaf_NoRandom)
	POSS E 6
	Goto See
  3ShotBurst:
    POSS A 0 A_Jump(128, "1shot")
    POSS E 10 A_FaceTarget
	POSS F 4 Bright A_CustomBulletAttack(12.5, 12.5, 1, Random(3, 9), "MonsterBulletPuff", 0, Cbaf_NoRandom)
	POSS E 4 A_FaceTarget
	POSS F 4 Bright A_CustomBulletAttack(15, 15, 1, Random(3, 9), "MonsterBulletPuff", 0, Cbaf_NoRandom)
	POSS E 4 A_FaceTarget
	POSS F 4 Bright A_CustomBulletAttack(17.5, 17.5, 1, Random(3, 9), "MonsterBulletPuff", 0, Cbaf_NoRandom)
	POSS E 4
	Goto See
  }
}

Actor Shotgunguy2 : Shotgunguy 
{
  PainChance 130
  Obituary "%o was blown apart by a shotgun zombie."
  States
  {
  See:
    SPOS AABBCCDD 3 A_Chase
	Loop
  Missile:
    SPOS EEEEE 2 A_FaceTarget
    SPOS F 10 Bright A_CustomBulletAttack(11.25, 11.25, 3, Random(1, 3)*5, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    SPOS E 8
    Goto See
  }
}

Actor Chaingunguy2 : Chaingunguy
{
  PainChance 130
  Obituary "A chaingunner made some swiss cheese out of %o."
  States
  {
  See:
    CPOS AABBCCDD 3 A_Chase
	Goto See
  Missile:
    CPOS E 10 A_FaceTarget 
    CPOS FE 4 Bright A_CustomBulletAttack(11.25, 11.25, 1, Random(1, 3)*5, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    CPOS F 1 A_CposRefire
    Goto Missile+1
  }
}
	
Actor Imp2 : DoomImp
{
  PainChance 125
  Obituary "%o played catch with an imp."
  HitObituary "An Imp lended %o a claw."
  +MissileMore
  Species "Imps"
  States
  {
  Melee:
    TROO A 0 A_FaceTarget //To make his recoiling less wierd
    TROO A 0 A_Recoil(-8)
	TROO EF 6 A_FaceTarget
	TROO G 6 A_CustomComboAttack("NewImpBall", 32, Random(4, 8)*3, "imp/melee")
	Goto See
  Missile:
    TROO EF 8 A_FaceTarget
	TROO G 6 A_CustomComboAttack("NewImpBall", 32, Random(4, 8)*3, "imp/melee")
	TROO A 0 A_Jump(128, "See")
	TROO EF 8 A_FaceTarget
	TROO G 6 A_CustomComboAttack("NewImpBall", 32, Random(4, 8)*3, "imp/melee")
	Goto See
  }
}

Actor NewImpBall : DoomImpBall
{
  Decal DoomImpScorch
  DamageType "Monster"
  States
  {
  Spawn:
    BAL1 AB 4 Bright A_SpawnItemEx("NewImpBallTrail", 0, 0, 0)
	Loop
  }
}

Actor NewImpBallTrail
{
  +NoInteraction
  RenderStyle Add
  +ClientSideOnly
  Alpha 0.5
  Scale 0.75
  States
  {
  Spawn:
    BAL1 CDE 6 Bright A_FadeOut(0.075)
	Stop
  }
}

Actor Pinky : Demon
{
  MeleeRange 72
  PainChance 100
  Speed 12
  Obituary "%o became a tasty snack for a pinky."
  +DontHurtSpecies
  Species "Pinkies"
  States
  {
  Melee:
    SARG A 0 A_FaceTarget //To make his recoiling less wierd
    SARG A 0 A_JumpIfCloser(64, "NOMNOMNOM")
    SARG A 0 A_Recoil(-10)
    SARG EF 6 A_FaceTarget
    SARG G 8 A_SargAttack
    Goto See
  NOMNOMNOM:
    SARG A 0 A_Recoil(-7.5)
	SARG EF 3 A_FaceTarget
	SARG G 4 A_CustomMeleeAttack(1.7 * Random(1, 10), "PsxPinky/Attack")
	Goto See
  }
}
    
Actor Spectre2 : Pinky
{
  Alpha 0.35
  Obituary "%o didn't quite see the spectre."
  RenderStyle Translucent
  Translation "16:31=96:107", "32:35=108:111", "36:43=5:8", "44:47=0:0", "79:79=0:0"
}

Actor LostSoul2 : Lostsoul replaces Lostsoul
{
  Scale 0.9
  Obituary "%o got himself a new head thanks to a lost soul."
  +NoBlood
  +NoBloodDecals
  +QuickToRetaliate
}

Actor Cacodemon2 : Cacodemon
{
  BloodColor Blue
  PainChance 96
  Obituary "%o was purified by a cacodemon." //Since his attack is like the cleric's second weapon from hexen.
  HitObituary "%o wanted to know what's inside of a cacodemon's belly."
  Species "Cacoes"
  States
  {
  Missile:
	HEAD BC 5 A_FaceTarget
	HEAD D 0 A_CustomMissile("CacodemonWeaverBall2", 32, 0, 0)
	HEAD D 5 Bright A_CustomComboAttack("CacodemonWeaverBall1", 32, 10 * random(1, 6))
	Goto See
  Melee:
    HEAD A 0 A_PlaySound("Demon/Melee")
    HEAD A 0 A_FaceTarget
	HEAD A 0 A_Recoil(-10)
	Goto Missile
  }
}

Actor CacodemonWeaverBall2 : CacodemonBall
{
  Decal CacoScorch
  DamageType "Monster"
  Damage 4
  Speed 15
  States
  {
  Spawn:
    BAL2 ABAB 1 Bright A_Weave(3, 0, 1.0, 0.0)
	BAL2 A 0 A_SpawnItemEx("CacoBallTrail")
	Loop
  }
}

Actor CacodemonWeaverBall1 : CacodemonWeaverBall2
{
  States
  {
  Spawn:
    BAL2 ABAB 1 Bright A_Weave(-3, 0, 1.0, 0.0)
	BAL2 A 0 A_SpawnItemEx("CacoBallTrail")
	Loop
  }
}

Actor CacoBallTrail : NewImpBallTrail
{
  States
  {
  Spawn:
    BAL2 AAABBB 1 Bright A_FadeOut(0.075)
	Stop
  }
}

Actor PainElemental2 : PainElemental replaces PainElemental
{
  +NoBlood
  +NoBloodDecals
  DamageFactor "PainElementalProjectile", 0.0
  MeleeRange 96
  Obituary "%o found out that pain elemental's aren't defense-less."
  States
  {
  Missile:
    PAIN DE 5 A_FaceTarget
	PAIN A 0 A_Jump(128, "SoulAttack")
    PAIN F 5 Bright A_FaceTarget
    PAIN F 0 Bright A_PainAttack("LostSoul2")
    Goto See
  Melee:
    PAIN A 0 A_PlaySound("Demon/Melee")
    PAIN DE 5 A_FaceTarget
	PAIN F 5 Bright A_CustomComboAttack("LostSoulProjectile", 32, Random(4, 6)*6, "Demon/Melee")
	Goto See
  SoulAttack:
	PAIN F 6 Bright A_CustomMissile("LostSoulProjectile", 32, 0, 0)
	Goto See
  }
}

Actor LostSoulProjectile : DoomImpBall
{
  Decal Scorch
  Scale 0.75
  Speed 15
  Damage 0
  RenderStyle SoulTrans
  +ForceRadiusDmg
  DamageType "PainElementalProjectile"
  States
  {
  Spawn:
    SKUL CD 4 Bright 
	Loop
  Death:
    SKUL A 0 A_Explode(45, 96)
    SKUL FGHIJK 6 Bright A_FadeOut(0.075)
	Stop
  }
}

Actor HellKnight2 : HellKnight replaces HellKnight
{
  BloodColor DarkGreen
  Obituary "%o was melted by a hellknight."
  HitObituary "%o was slain by a hellknight."
  DamageFactor "BaronMeteor", 0
  Species "Nobles"
  States
  {
  Missile:
    BOS2 EF 8 A_FaceTarget
    BOS2 G 8 A_CustomComboAttack("NewBaronBall", 32, Random(1, 8)*6, "Baron/Melee")
    Goto See 
  }
}

Actor NewBaronBall : BaronBall
{
  Decal BaronScorch
  DamageType "Monster"
  Damage (Random(37, 56))
  States
  {
  Spawn:
    BAL7 A 0
    BAL7 AB 1 A_SpawnItemEx("BaronTrail")
	Goto Spawn+1
  }
}

Actor BaronTrail : NewImpBallTrail
{
  Alpha 0.75
  States
  {
  Spawn:
    BAL7 ABABAB 1 Bright A_FadeOut(0.07)
	Stop
  }
}

Actor BaronOfHell2 : BaronOfHell
{
  BloodColor DarkGreen
  Scale 1.05
  Speed 9
  Obituary "%o got annihilated by a baron."
  HitObituary "A baron showed %o what it's like to get berserk punch'd."
  +QuickToRetaliate
  +DonthurtSpecies
  DamageFactor "BaronMeteor", 0
  Species "Nobles"
  States
  {
  Missile:
	BOSS EF 8 A_FaceTarget
    BOSS G 8 A_CustomComboAttack("NewBaronBall", 32, Random(1, 8)*6, "Baron/Melee")
	Goto See
  Death:
    BOSS I 8
    BOSS J 8 A_Scream
    BOSS K 8
    BOSS L 8 A_NoBlocking
    BOSS MN 8
    BOSS O -1 A_KillMaster
    Stop
  }
}

Actor Revenant2 : Revenant replaces Revenant
{
  Scale 0.93
  Obituary "%o was blown to hell by a revenant."
  HitObituary "%o got knocked out by a revenant."
  +QuickToRetaliate
  States
  {
  Missile:
    SKEL J 8 Bright A_FaceTarget
    SKEL K 8 A_CustomMissile("NewRevenantRocket", 48, 8, 0)
    SKEL K 8 A_FaceTarget
	SKEL A 0 A_Jump(64, "See")
	SKEL J 8 Bright A_FaceTarget
    SKEL K 8 A_CustomMissile("NewRevenantRocket", 48, -8, 0)
    SKEL K 8 A_FaceTarget
    Goto See
  }
}

Actor NewRevenantRocket : RevenantTracer
{
  Decal RevenantScorch
  Damage (Random(34, 52))
  DamageType "Monster"
  States
  {
  Spawn:
    FATB ABAB 2 Bright
	FATB A 0 A_Jump(256, "Homing", "NotHoming")
  Homing:
    FATB A 2 Bright A_Tracer2
	FATB A 0 A_SpawnItemEx("RevenantRocketTrail", 0, 0, 4)
	FATB B 2 Bright A_Tracer2
	FATB B 0 A_SpawnItemEx("RevenantRocketTrail", 0, 0, 4)
	Loop
  NotHoming:
    FATB AB 2 Bright A_SpawnItemEx("RevenantRocketTrail", 0, 0, 8)
	Loop
  }
}

Actor RevenantRocketTrail
{
  +ClientSideOnly
  +NoInteraction
  States
  {
  Spawn:
    PUFF BBBBCCCCDDDD 1 A_FadeOut(0.0417)
	Stop
  }
}

Actor Mancubus : Fatso
{
  Scale 1.075
  Obituary "A mancubus made %o all crispy."
  Species "Fatties"
  DamageFactor "TormentBfg", 0
  States
  {
  Missile:
    FATT G 20 A_FatRaise
	FATT H 0 Bright A_CustomMissile("NewFatShot", 24, -20, 12)
    FATT H 10 Bright A_CustomMissile("NewFatShot", 24, 20, 0)
    FATT IG 5 A_FaceTarget
    FATT H 0 Bright A_CustomMissile("NewFatShot", 24, -20, 0)
    FATT H 10 Bright A_CustomMissile("NewFatShot", 24, 20, -12)
    FATT IG 5 A_FaceTarget
    FATT H 0 Bright A_CustomMissile("NewFatShot", 24, -20, 12)
    FATT H 10 Bright A_CustomMissile("NewFatShot", 24, 20, -12)
    FATT IG 5 A_FaceTarget
    Goto See
  Death:
    FATT K 6
    FATT L 6 A_Scream
    FATT M 6 A_NoBlocking
    FATT NOPQRS 6
    FATT T -1 A_KillMaster
    Stop
  }
}

Actor NewFatShot : FatShot
{
  Decal Scorch
  DamageType "Monster"
  States
  {
  Spawn:
    MANF AB 2 Bright
	Loop
  }
}

Actor Arachnotron2 : Arachnotron
{
  Scale 1.075
  Speed 14
  PainChance 96
  +QuickToRetaliate
  Obituary "%o didn't stand a chance against an arachnotron."
  Species "Arachnotrons"
  States
  {
  Missile:
    BSPI A 15 A_FaceTarget
	BSPI A 0 A_JumpIfCloser(512, "Innacurate")
    BSPI G 4 Bright A_CustomMissile("NewArachPlasma", 32, 0, 0)
    BSPI H 4 Bright
    BSPI H 1 Bright A_SpidRefire
    Goto Missile+1
  Innacurate:
    BSPI G 4 Bright A_CustomMissile("NewArachPlasma", 32, 0, Random(12, -12))
    BSPI H 3 Bright
    BSPI H 1 Bright A_SpidRefire
    Goto Missile+1
  Pain:
    BSPI I 2
    BSPI I 2 A_Pain
    Goto See+1
  Death:
    BSPI J 20 A_Scream
    BSPI K 7 A_NoBlocking
    BSPI LMNO 7
    BSPI P -1 A_KillMaster
    Stop
  }
}

Actor NewArachPlasma : ArachnotronPlasma
{
  Decal NewArachScorch
  DamageType "Monster"
  States
  {
  Spawn:
    TNT1 A 1 Bright A_SpawnItemEx("ArachTrail")
    Loop
  }
}

Actor ArachTrail : NewArachPlasma
{
  +NoInteraction
  +ClientSideOnly
  Scale 0.4
  States
  {
  Spawn:
    APBX ABCDE 2 A_FadeOut(0.15)
	Stop
  }
}

Actor Archvile2 : Archvile replaces archvile
{
  Obituary "%o burnt to a crisp by an archvile."
  States
  {
  Missile:
    VILE G 0 Bright A_VileStart
    VILE G 10 Bright A_FaceTarget
    VILE H 8 Bright A_VileTarget("NewVileFire")
    VILE IJKLMN 8 Bright A_FaceTarget
    VILE O 8 Bright A_VileAttack
    VILE P 20 Bright
    Goto See
  }
}

Actor NewVileFire : ArchvileFire
{
  +ClientSideOnly
  States
  {
  Spawn:
    TNT1 A 0
	TNT1 A 0 A_PlaySound("vile/firestrt")
	TNT1 A 0 A_JumpIfInventory("FireToken", 30, "Death")
    TNT1 A 1 A_Fire
	TNT1 A 1 A_SpawnItemEx("VileFireEffect", Random(16, -16), Random(16, -16), Random(16, 48))
	TNT1 A 0 A_GiveInventory("FireToken", 1)
	Goto Spawn+2
  Death:
    TNT1 A 0
    Stop
  }
}

Actor VileFireEffect : NewVileFire
{
  Alpha 0.75
  Scale 0.25
  States
  {
  Spawn:
    FIRE A 0 
    FIRE A 2 Bright A_FireCrackle
	FIRE BCDEFGH 2 Bright A_FadeOut(0.025)
	Stop
  }
}

Actor FireToken : Inventory { Inventory.MaxAmount 30 }
	
Actor Spidermastermind2 : Spidermastermind replaces Spidermastermind
{
  Scale 1.1
  +QuickToRetaliate
  Obituary "%o was outsmarted by a spidermastermind."
  DropItem "ShieldAmmo", 256, 5
  States
  {
  See:
    SPID A 2 A_Metal
    SPID ABB 2 A_Chase
    SPID C 2 A_Metal
    SPID CDD 2 A_Chase
    SPID E 2 A_Metal
    SPID EFF 2 A_Chase
    Loop
  Missile:
    SPID A 15 A_FaceTarget
    SPID G 4 Bright A_CustomBulletAttack(22.5, 0, 4, Random(1, 5)*3, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    SPID H 3 Bright A_CustomBulletAttack(22.5, 0, 4, Random(1, 5)*3, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    SPID H 1 Bright A_SpidRefire
    Goto Missile+1
  Pain:
    SPID I 2
    SPID I 2 A_Pain
    Goto See
  }
}

Actor CyberDemon2 : Cyberdemon replaces Cyberdemon
{
  Scale 1.1
  +QuickToRetaliate
  Obituary "%o rode a cyberdemon's rocket."
  DropItem "ShieldAmmo", 256, 5
  States
  {
  Missile:
    CYBR E 6 A_FaceTarget
    CYBR F 12 Bright A_CustomMissile("CybRocket", 32, 0, 0)
    CYBR E 11 A_FaceTarget
    CYBR F 11  Bright A_CustomMissile("CybRocket", 32, 0, Random(2, -2))
    CYBR E 10 A_FaceTarget
    CYBR F 10  Bright A_CustomMissile("CybRocket", 32, 0, Random(4, -4))
	CYBR E 9 A_FaceTarget
    CYBR F 9  Bright A_CustomMissile("CybRocket", 32, 0, Random(6, -6))
    CYBR E 8 A_FaceTarget
    CYBR F 8  Bright A_CustomMissile("CybRocket", 32, 0, Random(8, -8))
    Goto See
  }
}

Actor CybRocket : Rocket
{
  Decal BigScorch
  DamageType "CyberRocket"
  States
  {
  Spawn:
    MISL A 4 Bright A_SpawnItemEx("CyRocketTrail", -8, 0, 6)
	Loop
  }
}
    
Actor CyRocketTrail : NewImpBallTrail
{
  Scale 0.5
  Alpha 0.25
  Translation "168:191=96:111"
}

Actor WolfensteinSS2 : Wolfensteinss replaces wolfensteinss
{
  Obituary "%o was shot on sight by a nazi SS."
  States
  {
  Missile:
    SSWV E 10 A_FaceTarget
    SSWV F 10 A_FaceTarget
    SSWV G 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
	SSWV G 4 bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    SSWV F 6 A_FaceTarget
    SSWV G 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
	SSWV G 4 bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    SSWV F 1 A_CPosRefire
	Goto Missile+1
  }
}

Actor NewExplosiveBarrel : ExplosiveBarrel replaces ExplosiveBarrel
{
  Obituary "%o found out why barrels have warnings on them."
  BloodColor Green
  DamageType "BarrelExplosion"
}