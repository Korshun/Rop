//Decorate: Captain Toenail, edited by Neon
//Sounds: Croteam, ID Software
//Sprites: Id software, Captain Toenail

Actor ArachnoSentinel //has to NOT inherit the arachnotron, otherwise map07 breaks
{
  Health 500
  Scale 1.1
  PainChance 80
  radius 64
  height 64
  mass 600
  speed 12
  seesound "Sentinel/Sight"
  painsound "baby/pain"
  deathsound "Sentinel/Death"
  activesound "baby/active"
  decal "bulletchip"
  MONSTER
  +DontHurtSpecies
  Species "Arachnotrons"
  Decal BulletChip
  States
  {
  Spawn:
    CSPI AB 10 A_Look
    loop
  See:
    CSPI A 15
    CSPI A 3 A_BabyMetal
    CSPI ABBCC 3 A_Chase
    CSPI D 3 A_BabyMetal
    CSPI DEEFF 3 A_Chase
    goto See+1
  Missile:
    CSPI A 15 bright A_FaceTarget
    CSPI G 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
    CSPI G 2 bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "MonsterBulletPuff", 0, CBAF_NORANDOM)
	CSPI H 2 Bright
    CSPI G 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
    CSPI G 2 bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "MonsterBulletPuff", 0, CBAF_NORANDOM)
    CSPI H 2 Bright
    CSPI H 0 bright A_SpidRefire
    goto Missile+1
  Pain:
    CSPI I 3
    CSPI I 3 A_Pain
    goto See+1
  Death:
    CSPI J 20 A_Scream
    CSPI K 7 A_NoBlocking
    CSPI LMNO 7
    CSPI P -1 A_KillMaster
    stop
  Raise:
    CSPI PONMLKJ 5
    goto See+1
  }
}
  