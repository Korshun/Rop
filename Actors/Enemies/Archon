Actor ArchonOfHell : BaronOfHell2
{
  Health 1250
  Speed 10
  PainChance 40
  Obituary "An archon turned %o into a pile of bones."
  HitObituary "%o was ripped to pieces by an archon."
  DropItem "ShieldAmmo", 192, 5
  SeeSound "PsxBaron/sight"
  PainSound "PsxDemon/pain"
  DeathSound "Archon/death"
  ActiveSound "PsxDemon/Act"
  +MissileMore
  DamageFactor "BaronMeteor", 0
  States
  {
  Spawn: 
    BELP AB 10 A_Look 
    Loop 
  See:
    BELP AABBCCDD 3 A_Chase 
    Loop 
  Missile:
    BELP A 0 A_Jump(128, "AltAttack")
  Melee:
    BELP EF 8 Bright A_FaceTarget 
    BELP G 4 Bright A_CustomComboAttack("BaronMeteor", 32, Random(32, 64), "BaronMelee")
    BELP H 4 Bright
	BELP IJ 8 Bright A_FaceTarget 
    BELP K 4 Bright A_CustomComboAttack("BaronMeteor", 32, Random(32, 64), "BaronMelee")
    BELP L 4 Bright
	Goto See
  AltAttack:
    BELP EF 8 Bright A_FaceTarget
	BELP G 0 Bright A_CustomMissile("NewBaronBall", 32, 0, 12)
	BELP G 4 Bright A_CustomMissile("NewBaronBall", 32, 0, 0)
	BELP H 4 Bright
	BELP IJ 8 Bright A_FaceTarget
	BELP K 4 Bright A_CustomMissile("NewBaronBall3", 32, 0, -12)
	BELP K 4 Bright A_CustomMissile("NewBaronBall3", 32, 0, 0)
	BELP L 4 Bright
	BELP MN 8 Bright A_FaceTarget
	BELP O 0 Bright A_CustomMissile("BaronMeteor", 32, 0, 0)
	BELP O 0 Bright A_CustomMissile("NewBaronBall", 32, 0, 12)
	BELP O 4 Bright A_CustomMissile("NewBaronBall", 32, 0, -12)
	BELP P 4 Bright
	Goto See
  Pain: 
    BELP Q 2 
    BELP Q 2 A_Pain 
    Goto See 
  Death: 
    BELP R 6 Bright 
    BELP S 6 Bright A_Scream 
    BELP T 6 Bright 
    BELP U 6 Bright A_NoBlocking 
    BELP VW 6 Bright 
    BELP X 6 Bright A_CustomMissile("ArchonSoul",86,0,0,2,90)
    BELP YZ 6 Bright 
    BELP [ 6 Bright
	TNT1 A -1 A_KillMaster
    Stop
  Raise:
    BELP [ 6 Bright
	BELP ZYXWVUTSR 6 Bright
	Goto See
  } 
}

Actor ArchonSoul
{   
  +NoInteraction
  +ClientSideOnly
  Speed 3
  RenderStyle Add
  Alpha 0.80
  States
  {
  Spawn:
    BFX1 ABCD 4 Bright A_FadeOut(0.1)
    Stop 
  }
}

Actor NewBaronBall3 : NewBaronBall { Speed 20 }

Actor BaronMeteor2 : BaronMeteor { Speed 20 }