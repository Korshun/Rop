Actor NewPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
  Weapon.AmmoType "PlasmaClip"
  Weapon.AmmoType2 "Cell"  
  Weapon.AmmoUse 1
  Weapon.AmmoGive2 40
  Weapon.AmmoGive 0
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.NOALERT
  Weapon.SlotNumber 6
  Weapon.PreferredSkin "PlasmaMarine"
  States
  {
  Deselect:
    PLSG A 1 A_Lower
	PLSG A 0 A_Lower
    Loop
  Select:
    PLSG A 1 A_Raise
	PLSG A 0 A_Raise
    Loop
  Fire:
    PLSG A 0 A_JumpIfNoAmmo("AltFire")
    PLSG A 0 A_GunFlash
	PLSG A 0 A_AlertMonsters
    PLSG A 3 A_FireCustomMissile("NewPlasmaBall", 0, 1)
    PLSG B 20 A_ReFire
    Goto Ready
  Altfire:
    TNT1 A 0 A_JumpIfInventory("PlasmaClip",50,2)
    TNT1 A 0 A_JumpIfInventory("Cell",1,2)
	TNT1 A 0
	Goto Ready
	PLSG B 2 OffSet(0, 38)
	PLSG B 2 OffSet(1, 44)
	PLSG B 2 OffSet(1, 52)
	PLSG B 2 OffSet(2, 60)
	PLSG B 2 OffSet(3, 72)
	PLSG B 12 A_OpenShotgun2
	PLSG B 0 A_JumpIfInventory("Cell",1,"Reloadin")
	PLSG B 12 A_LoadShotgun2
	PLSG B 12 A_PlaySound ("weapons/sshotc", CHAN_WEAPON)
	PLSG B 2 OffSet(3, 72)
	PLSG B 2 OffSet(2, 60)
	PLSG B 2 OffSet(1, 52)
	PLSG B 2 OffSet(1, 44)
	PLSG B 2 OffSet(0, 38)
	Goto Ready
  Reloadin:
	TNT1 A 0 A_TakeInventory("Cell",1)
	TNT1 A 0 A_GiveInventory("PlasmaClip",1)
	TNT1 A 0 A_JumpIfInventory("PlasmaClip",50,"Full")
	TNT1 A 0 A_JumpIfInventory("Cell",1,"Reloadin")
	Goto AltFire+10
  Full:
    TNT1 A 0
    Goto AltFire+10
  }
}

Actor NewPlasmaBall : PlasmaBall
{
  Decal NewPlasmaScorch
  +MThruSpecies
  +ThruGhost
  States
  {
  Spawn:
    TNT1 A 0 
	TNT1 A 1 A_SpawnItemEx("NewPlasmaTrail")
	Loop
  }
}

Actor NewPlasmaTrail
{
  +NoInteraction
  +ClientSideOnly
  Scale 0.4
  Alpha 0.5
  States
  {
  Spawn:
    PLSE BCDE 2 Bright A_FadeOut(0.09)
	Stop
  }
}

ACTOR PlasmaClip : Ammo
{
  Inventory.Amount 0
  Inventory.MaxAmount 50
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 50
  Inventory.Icon "CELLA0"
  +IGNORESKILL
}